import os
env = Environment(ENV=os.environ)
python = Builder(action="python $SOURCE")
builders = {"Python": python}
env.Append(BUILDERS=builders)

def analysis(name, additional_dependencies=None, alias=False):
    dependencies = ["analyses/{}.py".format(name)]
    if additional_dependencies:
        dependencies.extend(additional_dependencies)
    env.Python("results/{}.csv".format(name), dependencies)
    if alias:
        env.Alias(name, "results/{}.csv".format(name))

def plot(name, additional_dependencies):
    targets = ["figures/{}.pdf".format(name), "figures/{}.png".format(name)]
    dependencies = ["plots/{}.py".format(name)]
    if additional_dependencies:
        dependencies.extend(additional_dependencies)
    env.Python(targets, dependencies)
    env.Alias(name, targets)

analysis("num_participants", alias=True)
analysis("condition_counts", alias=True)
analysis("payrate", alias=True)
analysis("trial_order")
analysis("num_chance", alias=True)

analysis("fall_responses")
analysis("fall_response_corrs", ["results/fall_responses.csv"], alias=True)
analysis("fall_responses_best_parameters")
plot("fall_responses_best_parameters", ["results/fall_responses_best_parameters.csv"])

analysis("model_belief", ["results/trial_order.csv"])
analysis("model_belief_agg_all_params", ["results/model_belief.csv"])
analysis("model_belief_agg", ["results/model_belief_agg_all_params.csv"])
analysis("model_log_lh", ["results/model_belief_agg.csv"], alias=True)

analysis("mass_accuracy", ["results/model_belief_agg.csv"], alias=True)
analysis("mass_accuracy_best_parameters")
plot("mass_accuracy_best_parameters", ["results/mass_accuracy_best_parameters.csv"])
analysis("mass_accuracy_by_stimulus", ["results/model_belief_agg.csv"])
plot("mass_accuracy_by_stimulus", ["results/mass_accuracy_by_stimulus.csv"])
analysis("mass_accuracy_by_stimulus_corrs", ["results/mass_accuracy_by_stimulus.csv"], alias=True)
analysis("mass_accuracy_by_trial", ["results/model_belief_agg.csv"])
plot("mass_accuracy_by_trial", ["results/mass_accuracy_by_trial.csv"])
plot("mass_accuracy_by_trial_with_model", ["results/mass_accuracy_by_trial.csv"])
analysis("mass_accuracy_by_trial_corrs", ["results/mass_accuracy_by_trial.csv"], alias=True)
analysis("mass_responses_by_stimulus", ["results/model_belief_agg.csv"])
plot("mass_responses_by_stimulus", ["results/mass_responses_by_stimulus.csv"])
analysis("mass_responses_by_stimulus_corrs", ["results/mass_responses_by_stimulus.csv"], alias=True)
analysis("fit_mass_responses", ["results/mass_responses_by_stimulus.csv"])
plot("fit_mass_responses", ["results/mass_responses_by_stimulus.csv", "results/fit_mass_responses.csv"])

analysis("switchpoint")
analysis("num_learned_by_trial", ["results/switchpoint.csv", "results/mass_accuracy.csv"])
plot("num_learned_by_trial", ["results/num_learned_by_trial.csv"])
analysis("participant_fits", ["results/model_log_lh.csv"], alias=True)
